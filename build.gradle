plugins {
    id 'java-library'
    id 'maven-publish'
}

import org.gradle.internal.os.OperatingSystem

project.ext.lwjglVersion = "3.3.3"
project.ext.jomlVersion = "1.10.5"

switch (OperatingSystem.current()) {
    case OperatingSystem.LINUX:
        project.ext.lwjglNatives = "natives-linux"
        def osArch = System.getProperty("os.arch")
        if (osArch.startsWith("arm") || osArch.startsWith("aarch64")) {
            project.ext.lwjglNatives += osArch.contains("64") || osArch.startsWith("armv8") ? "-arm64" : "-arm32"
        } else if  (osArch.startsWith("ppc")) {
            project.ext.lwjglNatives += "-ppc64le"
        } else if  (osArch.startsWith("riscv")) {
            project.ext.lwjglNatives += "-riscv64"
        }
        break
    case OperatingSystem.MAC_OS:
        project.ext.lwjglNatives = "natives-macos"
        break
    case OperatingSystem.WINDOWS:
        project.ext.lwjglNatives = "natives-windows"
        break
}

project.ext.projectName = "comet-renderer"
version = project.main_version

apply from: file("utils.gradle")

void subSourceSet(String name) {
    sourceSets.main.java.srcDirs += "src/renderer/" + name + "/java"
    sourceSets.main.resources.srcDirs += "src/renderer/" + name + "/resources"
}

sourceSets {
    main {
        java.srcDirs = ["src/main/main/java"]
        resources.srcDirs = ["src/main/main/resources"]
    }

    subSourceSet("main")
    subSourceSet("buffer")
    subSourceSet("exceptions")
    subSourceSet("program")
    subSourceSet("vertex")
    subSourceSet("sampler")
    subSourceSet("builders")
    subSourceSet("utils")
}

dependencies {
    implementation "com.ferra13671:gl-texture-utils:${gl_texture_utils_version}"

    compileOnly files("libs/FerraGuard-Annotations-2.0.jar")

    implementation platform("org.lwjgl:lwjgl-bom:$lwjglVersion")



    implementation "org.lwjgl:lwjgl"
    implementation "org.lwjgl:lwjgl-jawt"
    implementation "org.lwjgl:lwjgl-opengl"
    implementation "org.lwjgl:lwjgl-stb"
    implementation "org.joml:joml:${jomlVersion}"

    runtimeOnly "org.lwjgl:lwjgl::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-opengl::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-stb::$lwjglNatives"
}