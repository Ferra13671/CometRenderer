plugins {
    id 'java-library'
    id 'fabric-loom' version '1.11-SNAPSHOT'
    id 'maven-publish'
}

version = "1.6"
group = "com.ferra13671"

base {
    archivesName = "CometRenderer"
}

void registerSourceSet(String name) {
    sourceSets.renderer.java.srcDirs += "src/renderer/" + name + "/java"

    jar {
        from('src/renderer/' + name + '/java') {
            include '**/*.java'
        }
    }
}

sourceSets {
    //SourceSets, хранящий в себе непосредственно сам рендерер
    renderer {
        java.srcDirs = ["src/renderer/main/java"]
        resources.srcDirs = ["src/renderer/main/resources"]
        compileClasspath += main.compileClasspath
    }
    //SourceSets, хранящий в себе программу для тестирования рендерера
    testmod {
        compileClasspath += renderer.compileClasspath
        runtimeClasspath += renderer.runtimeClasspath
        compileClasspath += main.compileClasspath
        runtimeClasspath += main.runtimeClasspath
    }

    registerSourceSet("main")
    registerSourceSet("framebuffer")
    registerSourceSet("exeptions")
    registerSourceSet("posteffect")
    registerSourceSet("program")
    registerSourceSet("vertex")
    registerSourceSet("utils")
}

//Добавляем в gradle проект задачу для запуска майнкрафта с программой для тестирования рендерера
loom {
    runs {
        testmodClient {
            client()
            name = "Testmod Client"
            source sourceSets.testmod
        }
    }
}

repositories {
    maven {
        name = 'ferra13671-maven'
        url = 'https://ferra13671.github.io/maven/'
    }
    mavenCentral()
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    implementation "com.ferra13671:gl-texture-utils:1.6.1"

    //Добавляем зависимости для плагина
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'

    compileOnly files("libs/FerraGuard-Annotations-2.0.jar")

    //Добавляем в зависимости для программы для тестирования рендерера
    testmodImplementation sourceSets.renderer.output
    testmodImplementation sourceSets.main.output
}

test {
    useJUnitPlatform()
}
