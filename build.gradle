plugins {
    id 'java-library'
    id 'fabric-loom' version '1.11-SNAPSHOT'
    id 'maven-publish'
}

version = "1.8.4-public"
group = "com.ferra13671"

java {
    withSourcesJar()
}

base {
    archivesName = "comet-renderer"
}

void subSourceSet(String name) {
    sourceSets.main.java.srcDirs += "src/renderer/" + name + "/java"
    sourceSets.main.resources.srcDirs += "src/renderer/" + name + "/resources"
}

sourceSets {
    main {
        java.srcDirs = ["src/main/main/java"]
        resources.srcDirs = ["src/main/main/resources"]
    }
    testmod {
        compileClasspath += main.compileClasspath
        runtimeClasspath += main.runtimeClasspath
    }

    subSourceSet("main")
    subSourceSet("buffer")
    subSourceSet("exceptions")
    subSourceSet("posteffect")
    subSourceSet("program")
    subSourceSet("vertex")
    subSourceSet("builders")
    subSourceSet("utils")
}

loom {
    runs {
        testmodClient {
            client()
            name = "Testmod Client"
            source sourceSets.testmod
        }
    }
}

repositories {
    maven {
        name = 'ferra13671-maven'
        url = 'https://ferra13671.github.io/maven/'
    }
    mavenCentral()
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    implementation "com.ferra13671:gl-texture-utils:1.6.1"

    compileOnly files("libs/FerraGuard-Annotations-2.0.jar")

    testmodImplementation sourceSets.main.output
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'com.ferra13671'
            artifactId = 'comet-renderer'
            artifact source: jar, classifier: null
            artifact sourcesJar
        }
    }

    repositories {
        mavenLocal()
    }
}
